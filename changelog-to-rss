#! /usr/bin/awk -f

# this converts entries in the CHANGELOG into RSS items

function printitem () {
	if (itemdate == "") {
		return;
	}

	gsub (/&/, "&amp;", itemdesc);
	gsub (/</, "&lt;", itemdesc);
	gsub (/>/, "&gt;", itemdesc);

	printf ("<item>\n");
	printf ("<title>change by %s on %s</title>\n", itemauthor, itemdate);
	printf ("<description>%s</description>\n", itemdesc);
	printf ("</item>\n");
	itemdate = "";
}

function printrsshead () {
	printf ("<?xml version=\"1.0\"?>\n");
	printf ("<!-- RSS generated by changelog-to-rss -->\n");
	printf ("<rss version=\"2.0\">\n");
	printf ("<channel>\n");
	printf ("<title>NOCC changelog</title>\n");
	printf ("<link>http://www.cs.kent.ac.uk/projects/ofa/nocc/</link>\n");
	printf ("<description>NOCC changelog (a new occam-pi compiler)</description>\n");
	printf ("<language>en-gb</language>\n");
	printf ("<generator>changelog-to-rss</generator>\n");
	printf ("<webMaster>F.R.M.Barnes@kent.ac.uk</webMaster>\n");
	printf ("<ttl>240</ttl>\n");
}

function printrsstail () {
	printf ("</channel>\n");
	printf ("</rss>\n");
}

BEGIN {
	lno = 0;
	lastline = "";
	itemdate = "";
	itemauthor = "";
	itemdesc = "";

	printrsshead();
}

{
	lno++;
	if (lno > 1) {
		if (substr ($0, 1, 1) == "	") {
			# continuation of a previous item
			str = $0;
			sub (/^[\t ]*/, "", str)
			#while (substr (str, 1, 1) == "	") {
			#	str = substr (str, 2);
			#}
			if (substr (lastline, length (lastline), 1) == "-") {
				itemdesc = sprintf ("%s%s", itemdesc, str);
			} else {
				itemdesc = sprintf ("%s %s", itemdesc, str);
			}
		} else {
			# a new item -- output the last
			printitem();

			itemdate = $1;
			itemauthor = $2;
			itemdesc = "";
			i = 3;
			nfields = split ($0, AR, /[ \t]/);
			while (i <= nfields) {
				itemdesc = sprintf ("%s %s", itemdesc, AR[i]);
				i++;
			}
		}
		lastline = $0;

	}
}

END {
	printitem();
	printrsstail();
}

