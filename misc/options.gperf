%{
/*
 *	options.gperf -- command-line options for nocc
 */
%}
%struct-type
%language=ANSI-C
%define initializer-suffix ,'\0',NULL,NULL,NULL,-1
%define slot-name name
%define lookup-function-name option_lookup_byname
%null-strings
%global-table
%readonly-tables

struct TAG_cmd_option;
%%
help,			'h',	opt_do_help_flag,	NULL,				"0display standard usage information",			0
help-ful,		'\0',	opt_do_help_flag,	NULL,				"1display full usage information",			1
dump-specs,		'\0',	opt_setintflag,		&(compopts.dumpspecs),		"1print compiler specs",				2
dump-tree,		'\0',	opt_setintflag,		&(compopts.dumptree),		"1print parse tree",					3
dump-tree-to,		'\0',	opt_setstr,		&(compopts.dumptreeto),		"1print parse tree to file",				4
dump-stree,		'\0',	opt_setintflag,		&(compopts.dumpstree),		"1print parse tree in s-record format",			5
dump-stree-to,		'\0',	opt_setstr,		&(compopts.dumpstreeto),	"1print parse tree in s-record format to file",		6
dump-lexers,		'\0',	opt_setintflag,		&(compopts.dumplexers),		"1print registered languages (lexers)",			7
dump-grammar,		'\0',	opt_setintflag,		&(compopts.dumpgrammar),	"1print grammars during parser init",			8
dump-grules,		'\0',	opt_setintflag,		&(compopts.dumpgrules),		"1print generic reduction rules after parser init",	9
dump-dfas,		'\0',	opt_setintflag,		&(compopts.dumpdfas),		"1print named DFAs after parser init",			10
dump-fcns,		'\0',	opt_setintflag,		&(compopts.dumpfcns),		"1print registered functions",				11
dump-names,		'\0',	opt_setintflag,		&(compopts.dumpnames),		"1print names after scope",				12
dump-targets,		'\0',	opt_setintflag,		&(compopts.dumptargets),	"1print supported targets after initialisation",	13
dump-varmaps,		'\0',	opt_setintflag,		&(compopts.dumpvarmaps),	"1print variable maps after block allocation",		14
dump-nodetypes,		'\0',	opt_setintflag,		&(compopts.dumpnodetypes),	"1print node types after initialisation",		15
dump-snodetypes,	'\0',	opt_setintflag,		&(compopts.dumpsnodetypes),	"1print node types after initialisation (short form)",	16
dump-snodetags,		'\0',	opt_setintflag,		&(compopts.dumpsnodetags),	"1print node tags after initialisation (short form)",	17
dump-chooks,		'\0',	opt_setintflag,		&(compopts.dumpchooks),		"1print compiler hooks",				18
dump-extns,		'\0',	opt_setintflag,		&(compopts.dumpextns),		"1print detailed information about loaded extensions",	19
dump-folded,		'\0',	opt_setintflag,		&(compopts.dumpfolded),		"1include folds in parse tree dumps",			20
save-named-dfa,		'\0',	opt_setsaveopt,		(void *)1,			"1save named DFA to file",				50
save-all-dfas,		'\0',	opt_setstr,		&(compopts.savealldfas),	"1save all DFAs to file",				51
stop-token,		'\0',	opt_setstopflag,	(void *)1,			"1stop after tokenise (and print tokens)",		100
stop-parse,		'\0',	opt_setstopflag,	(void *)2,			"1stop after parse",					101
stop-prescope,		'\0',	opt_setstopflag,	(void *)3,			"1stop after pre-scope",				102
stop-scope,		'\0',	opt_setstopflag,	(void *)4,			"1stop after scope",					103
stop-typecheck,		'\0',	opt_setstopflag,	(void *)5,			"1stop after type check",				104
stop-constprop,		'\0',	opt_setstopflag,	(void *)6,			"1stop after constant propagation",			105
stop-typeresolve,	'\0',	opt_setstopflag,	(void *)7,			"1stop after type resolve",				106
stop-precheck,		'\0',	opt_setstopflag,	(void *)8,			"1stop after pre-check",				107
stop-aliascheck,	'\0',	opt_setstopflag,	(void *)9,			"1stop after alias check",				108
stop-usagecheck,	'\0',	opt_setstopflag,	(void *)10,			"1stop after parallel-usage check",			109
stop-undefcheck,	'\0',	opt_setstopflag,	(void *)11,			"1stop after undefined-usage check",			110
stop-postcheck,		'\0',	opt_setstopflag,	(void *)12,			"1stop after post-check",				111
stop-fetrans,		'\0',	opt_setstopflag,	(void *)13,			"1stop after front-end tree transform",			112
stop-betrans,		'\0',	opt_setstopflag,	(void *)14,			"1stop after back-end tree transform",			113
stop-namemap,		'\0',	opt_setstopflag,	(void *)15,			"1stop after name-map",					114
stop-prealloc,		'\0',	opt_setstopflag,	(void *)16,			"1stop after pre-allocation",				115
stop-alloc,		'\0',	opt_setstopflag,	(void *)17,			"1stop after variable allocation",			116
stop-codegen,		'\0',	opt_setstopflag,	(void *)18,			"1stop after code-generation",				117
skip-aliascheck,	'\0',	opt_clearintflag,	&(compopts.doaliascheck),	"1skip alias checks",					150
skip-usagecheck,	'\0',	opt_clearintflag,	&(compopts.dousagecheck),	"1skip parallel usage checks",				151
skip-defcheck,		'\0',	opt_clearintflag,	&(compopts.dodefcheck),		"1skip undefinedness checks",				152
trace-parser,		'\0',	opt_setintflag,		&(compopts.traceparser),	"1trace parser (debugging)",				200
trace-typecheck,	'\0',	opt_setintflag,		&(compopts.tracetypecheck),	"1trace type-check (debugging)",			201
trace-constprop,	'\0',	opt_setintflag,		&(compopts.traceconstprop),	"1trace constant-propagation (debugging)",		202
trace-precode,		'\0',	opt_setintflag,		&(compopts.traceprecode),	"1trace pre-code (debugging)",				203
treecheck,		'\0',	opt_setintflag,		&(compopts.treecheck),		"1enable run-time parse tree checking",			250
verbose,		'v',	opt_setintflag,		&(compopts.verbose),		"0verbose compilation",					251
debug-parser,		'\0',	opt_setintflag,		&(compopts.debugparser),	"1debug parser",					252
version,		'V',	opt_do_version,		NULL,				"0print version and exit",				253
compile,		'c',	opt_setintflag,		&(compopts.notmainmodule),	"0compile for separate compilation",			300
dump-dmem,		'\0',	opt_setintflag,		&(compopts.dmemdump),		"1display dynamic memory pool information",		301
gdb,			'\0',	opt_setintflag,		&(compopts.fatalgdb),		"1launch GDB on fatal error",				302
specs-file,		'\0',	opt_setstr,		&(compopts.specsfile),		"0path to compiler specs file",				303
output,			'o',	opt_setstr,		&(compopts.outfile),		"0output file-name",					304
include-path,		'I',	opt_addincludepath,	NULL,				"0add paths for include files",				350
library-path,		'L',	opt_addlibrarypath,	NULL,				"0add paths for library files",				351
extn-path,		'E',	opt_addextnpath,	NULL,				"1add paths for compiler extensions",			352
extn,			'e',	opt_addextn,		NULL,				"0compiler extension to load",				400
dump-tracemem,		'\0',	opt_setintflag,		&(compopts.dumptracemem),	"1display left-over memory blocks (if compiled)",	450
%%

