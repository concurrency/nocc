%{
/*
 *	options.gperf -- command-line options for nocc
 */
%}
%struct-type
%language=ANSI-C
%define initializer-suffix ,'\0',NULL,NULL,NULL,-1
%define slot-name name
%define lookup-function-name option_lookup_byname
%null-strings
%global-table
%readonly-tables

struct TAG_cmd_option;
%%
help,			'h',	opt_do_help_flag,	NULL,				"0display standard usage information",			0
help-ful,		'\0',	opt_do_help_flag,	NULL,				"1display full usage information",			1
dump-specs,		'\0',	opt_setintflag,		&(compopts.dumpspecs),		"1print compiler specs",				2
dump-tree,		'\0',	opt_setintflag,		&(compopts.dumptree),		"1print parse tree",					3
dump-tree-to,		'\0',	opt_setstr,		&(compopts.dumptreeto),		"1print parse tree to file",				4
dump-stree,		'\0',	opt_setintflag,		&(compopts.dumpstree),		"1print parse tree in s-record format",			5
dump-stree-to,		'\0',	opt_setstr,		&(compopts.dumpstreeto),	"1print parse tree in s-record format to file",		6
dump-grammar,		'\0',	opt_setintflag,		&(compopts.dumpgrammar),	"1print grammars during parser init",			7
dump-grules,		'\0',	opt_setintflag,		&(compopts.dumpgrules),		"1print generic reduction rules after parser init",	8
dump-dfas,		'\0',	opt_setintflag,		&(compopts.dumpdfas),		"1print named DFAs after parser init",			9
dump-names,		'\0',	opt_setintflag,		&(compopts.dumpnames),		"1print names after scope",				10
dump-targets,		'\0',	opt_setintflag,		&(compopts.dumptargets),	"1print supported targets after initialisation",	11
dump-varmaps,		'\0',	opt_setintflag,		&(compopts.dumpvarmaps),	"1print variable maps after block allocation",		12
dump-nodetypes,		'\0',	opt_setintflag,		&(compopts.dumpnodetypes),	"1print node types after initialisation",		13
dump-chooks,		'\0',	opt_setintflag,		&(compopts.dumpchooks),		"1print compiler hooks",				14
dump-extns,		'\0',	opt_setintflag,		&(compopts.dumpextns),		"1print detailed information about loaded extensions",	15
dump-folded,		'\0',	opt_setintflag,		&(compopts.dumpfolded),		"1include folds in parse tree dumps",			16
save-named-dfa,		'\0',	opt_setsaveopt,		(void *)1,			"1save named DFA to file",				17
save-all-dfas,		'\0',	opt_setstr,		&(compopts.savealldfas),	"1save all DFAs to file",				18
stop-token,		'\0',	opt_setstopflag,	(void *)1,			"1stop after tokenise (and print tokens)",		19
stop-parse,		'\0',	opt_setstopflag,	(void *)2,			"1stop after parse",					20
stop-prescope,		'\0',	opt_setstopflag,	(void *)3,			"1stop after pre-scope",				21
stop-scope,		'\0',	opt_setstopflag,	(void *)4,			"1stop after scope",					22
stop-typecheck,		'\0',	opt_setstopflag,	(void *)5,			"1stop after type check",				23
stop-constprop,		'\0',	opt_setstopflag,	(void *)6,			"1stop after constant propagation",			24
stop-precheck,		'\0',	opt_setstopflag,	(void *)7,			"1stop after pre-check",				25
stop-aliascheck,	'\0',	opt_setstopflag,	(void *)8,			"1stop after alias check",				26
stop-usagecheck,	'\0',	opt_setstopflag,	(void *)9,			"1stop after parallel-usage check",			27
stop-undefcheck,	'\0',	opt_setstopflag,	(void *)10,			"1stop after undefined-usage check",			28
stop-postcheck,		'\0',	opt_setstopflag,	(void *)11,			"1stop after post-check",				29
stop-fetrans,		'\0',	opt_setstopflag,	(void *)12,			"1stop after front-end tree transform",			30
stop-betrans,		'\0',	opt_setstopflag,	(void *)13,			"1stop after back-end tree transform",			31
stop-namemap,		'\0',	opt_setstopflag,	(void *)14,			"1stop after name-map",					32
stop-prealloc,		'\0',	opt_setstopflag,	(void *)15,			"1stop after pre-allocation",				33
stop-alloc,		'\0',	opt_setstopflag,	(void *)16,			"1stop after variable allocation",			34
stop-codegen,		'\0',	opt_setstopflag,	(void *)17,			"1stop after code-generation",				35
skip-aliascheck,	'\0',	opt_clearintflag,	&(compopts.doaliascheck),	"1skip alias checks",					36
skip-usagecheck,	'\0',	opt_clearintflag,	&(compopts.dousagecheck),	"1skip parallel usage checks",				37
skip-defcheck,		'\0',	opt_clearintflag,	&(compopts.dodefcheck),		"1skip undefinedness checks",				38
trace-parser,		'\0',	opt_setintflag,		&(compopts.traceparser),	"1trace parser (debugging)",				39
trace-typecheck,	'\0',	opt_setintflag,		&(compopts.tracetypecheck),	"1trace type-check (debugging)",			40
verbose,		'v',	opt_setintflag,		&(compopts.verbose),		"0verbose compilation",					41
debug-parser,		'\0',	opt_setintflag,		&(compopts.debugparser),	"1debug parser",					42
version,		'V',	opt_do_version,		NULL,				"0print version and exit",				43
compile,		'c',	opt_setintflag,		&(compopts.notmainmodule),	"0compile for separate compilation",			44
dump-dmem,		'\0',	opt_setintflag,		&(compopts.dmemdump),		"1display dynamic memory pool information",		45
specs-file,		'\0',	opt_setstr,		&(compopts.specsfile),		"0path to compiler specs file",				46
output,			'o',	opt_setstr,		&(compopts.outfile),		"0output file-name",					47
include-path,		'I',	opt_addincludepath,	NULL,				"0add paths for include files",				48
library-path,		'L',	opt_addlibrarypath,	NULL,				"0add paths for library files",				49
extn-path,		'E',	opt_addextnpath,	NULL,				"1add paths for compiler extensions",			50
extn,			'e',	opt_addextn,		NULL,				"0compiler extension to load",				51
%%

