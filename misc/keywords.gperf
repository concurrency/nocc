%{
/*
 *	keywords.gperf -- standard keywords for nocc
 *	Fred Barnes, 2005  <frmb@kent.ac.uk>
 */
%}
%struct-type
%language=ANSI-C
%define initializer-suffix ,-1,0,NULL
%define slot-name name
%define lookup-function-name keyword_lookup_byname
%null-strings
%global-table
%readonly-tables

struct TAG_keyword;
%%
SKIP,		0,	LANGTAG_OCCAMPI,	NULL
STOP,		1,	LANGTAG_OCCAMPI,	NULL
PAR,		2,	LANGTAG_OCCAMPI,	NULL
SEQ,		3,	LANGTAG_OCCAMPI,	NULL
PROC,		4,	LANGTAG_OCCAMPI,	NULL
BYTE,		5,	LANGTAG_OCCAMPI,	NULL
BOOL,		6,	LANGTAG_OCCAMPI,	NULL
INT,		7,	LANGTAG_OCCAMPI,	NULL
INT16,		8,	LANGTAG_OCCAMPI,	NULL
INT32,		9,	LANGTAG_OCCAMPI,	NULL
INT64,		10,	LANGTAG_OCCAMPI,	NULL
REAL32,		11,	LANGTAG_OCCAMPI,	NULL
REAL64,		12,	LANGTAG_OCCAMPI,	NULL
IS,		13,	LANGTAG_OCCAMPI,	NULL
OF,		14,	LANGTAG_OCCAMPI,	NULL
FUNCTION,	15,	LANGTAG_OCCAMPI,	NULL
CHAN,		16,	LANGTAG_OCCAMPI,	NULL
DATA,		17,	LANGTAG_OCCAMPI,	NULL
TYPE,		18,	LANGTAG_OCCAMPI,	NULL
AT,		19,	LANGTAG_OCCAMPI,	NULL
VAL,		20,	LANGTAG_OCCAMPI,	NULL
INLINE,		21,	LANGTAG_OCCAMPI,	NULL
FORK,		22,	LANGTAG_OCCAMPI,	NULL
MOBILE,		23,	LANGTAG_OCCAMPI,	NULL
BARRIER,	24,	LANGTAG_OCCAMPI,	NULL
SYNC,		25,	LANGTAG_OCCAMPI,	NULL
PROTOCOL,	26,	LANGTAG_OCCAMPI,	NULL
CASE,		27,	LANGTAG_OCCAMPI,	NULL
TRUE,		28,	LANGTAG_OCCAMPI,	NULL
FALSE,		29,	LANGTAG_OCCAMPI,	NULL
ELSE,		30,	LANGTAG_OCCAMPI,	NULL
TIMER,		31,	LANGTAG_OCCAMPI,	NULL
IF,		32,	LANGTAG_OCCAMPI,	NULL
SIZE,		33,	LANGTAG_OCCAMPI,	NULL
FROM,		34,	LANGTAG_OCCAMPI,	NULL
FOR,		35,	LANGTAG_OCCAMPI,	NULL
VALOF,		36,	LANGTAG_OCCAMPI,	NULL
RESULT,		37,	LANGTAG_OCCAMPI,	NULL
RETYPES,	38,	LANGTAG_OCCAMPI,	NULL
TRUNC,		39,	LANGTAG_OCCAMPI,	NULL
ROUND,		40,	LANGTAG_OCCAMPI,	NULL
MOSTPOS,	41,	LANGTAG_OCCAMPI,	NULL
MOSTNEG,	42,	LANGTAG_OCCAMPI,	NULL
CLONE,		43,	LANGTAG_OCCAMPI,	NULL
INITIAL,	44,	LANGTAG_OCCAMPI,	NULL
STEP,		45,	LANGTAG_OCCAMPI,	NULL
RECORD,		46,	LANGTAG_OCCAMPI,	NULL
ALT,		47,	LANGTAG_OCCAMPI,	NULL
PRI,		48,	LANGTAG_OCCAMPI,	NULL
AFTER,		49,	LANGTAG_OCCAMPI,	NULL
WHILE,		50,	LANGTAG_OCCAMPI,	NULL
AND,		51,	LANGTAG_OCCAMPI,	NULL
OR,		52,	LANGTAG_OCCAMPI,	NULL
PLUS,		53,	LANGTAG_OCCAMPI,	NULL
MINUS,		54,	LANGTAG_OCCAMPI,	NULL
NOT,		55,	LANGTAG_OCCAMPI,	NULL
TIMES,		56,	LANGTAG_OCCAMPI,	NULL
MOBILE.CHAN,	57,	LANGTAG_OCCAMPI,	NULL
MOBILE.DATA,	58,	LANGTAG_OCCAMPI,	NULL
MOBILE.PROC,	59,	LANGTAG_OCCAMPI,	NULL
ANY,		60,	LANGTAG_OCCAMPI,	NULL
PLACE,		61,	LANGTAG_OCCAMPI,	NULL
IN,		62,	LANGTAG_OCCAMPI,	NULL
WORKSPACE,	63,	LANGTAG_OCCAMPI,	NULL
VECSPACE,	64,	LANGTAG_OCCAMPI,	NULL
MOBSPACE,	65,	LANGTAG_OCCAMPI,	NULL
DEFINED,	66,	LANGTAG_OCCAMPI,	NULL
SHARED,		67,	LANGTAG_OCCAMPI,	NULL
CHAR,		68,	LANGTAG_OCCAMPI,	NULL
RESCHEDULE,	69,	LANGTAG_OCCAMPI,	NULL
SETPRI,		70,	LANGTAG_OCCAMPI,	NULL
PUBLIC,		71,	LANGTAG_OCCAMPI,	NULL
ASM,		72,	LANGTAG_OCCAMPI,	NULL
BYTESIN,	73,	LANGTAG_OCCAMPI,	NULL
GETPRI,		74,	LANGTAG_OCCAMPI,	NULL
TRACES,		75,	LANGTAG_OCCAMPI,	NULL
INTERLEAVE,	76,	LANGTAG_OCCAMPI,	NULL
PORT,		77,	LANGTAG_OCCAMPI,	NULL
PLACED,		78,	LANGTAG_OCCAMPI,	NULL
XOR,		79,	LANGTAG_OCCAMPI,	NULL
EXCEPTION,	80,	LANGTAG_OCCAMPI,	NULL
THROW,		81,	LANGTAG_OCCAMPI,	NULL
CATCH,		82,	LANGTAG_OCCAMPI,	NULL
TRY,		83,	LANGTAG_OCCAMPI,	NULL
FINALLY,	84,	LANGTAG_OCCAMPI,	NULL
NOEXCEPTIONS,	85,	LANGTAG_OCCAMPI,	NULL
EXTENDS,	86,	LANGTAG_OCCAMPI,	NULL
ASSERT,		87,	LANGTAG_OCCAMPI,	NULL
ON,		88,	LANGTAG_OCCAMPI,	NULL
VECTOR,		89,	LANGTAG_OCCAMPI,	NULL
UINT,		90,	LANGTAG_OCCAMPI,	NULL
UINT16,		91,	LANGTAG_OCCAMPI,	NULL
UINT32,		92,	LANGTAG_OCCAMPI,	NULL
UINT64,		93,	LANGTAG_OCCAMPI,	NULL
skip,		94,	LANGTAG_GUPPY,		NULL
stop,		95,	LANGTAG_GUPPY,		NULL
define,		96,	LANGTAG_GUPPY,		NULL
chan,		97,	LANGTAG_GUPPY,		NULL
val,		98,	LANGTAG_GUPPY,		NULL
int,		99,	LANGTAG_GUPPY | 0x00010000,		NULL
real,		100,	LANGTAG_GUPPY | 0x00010000,		NULL
byte,		101,	LANGTAG_GUPPY,		NULL
bool,		102,	LANGTAG_GUPPY,		NULL
char,		103,	LANGTAG_GUPPY,		NULL
string,		104,	LANGTAG_GUPPY,		NULL
type,		105,	LANGTAG_GUPPY,		NULL
seq,		106,	LANGTAG_GUPPY,		NULL
par,		107,	LANGTAG_GUPPY,		NULL
alt,		108,	LANGTAG_GUPPY,		NULL
pri,		109,	LANGTAG_GUPPY,		NULL
enum,		110,	LANGTAG_GUPPY,		NULL
uint,		111,	LANGTAG_GUPPY | 0x00010000,		NULL
public,		112,	LANGTAG_GUPPY,		NULL
for,		113,	LANGTAG_GUPPY,		NULL
size,		114,	LANGTAG_GUPPY,		NULL
throw,		115,	LANGTAG_GUPPY,		NULL
catch,		116,	LANGTAG_GUPPY,		NULL
protocol,	117,	LANGTAG_GUPPY,		NULL
subprotocol,	118,	LANGTAG_GUPPY,		NULL
end,		119,	LANGTAG_GUPPY,		NULL
bytesin,	120,	LANGTAG_GUPPY,		NULL
is,		121,	LANGTAG_GUPPY,		NULL
return,		122,	LANGTAG_GUPPY,		NULL
case,		123,	LANGTAG_GUPPY,		NULL
while,		124,	LANGTAG_GUPPY,		NULL
true,		125,	LANGTAG_GUPPY,		NULL
false,		126,	LANGTAG_GUPPY,		NULL
%%

